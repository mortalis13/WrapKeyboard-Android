// gradle-6.7
// jdk1.8.0_91

apply plugin: 'com.android.application'

buildscript {
  repositories {
    google()
    jcenter()
    mavenLocal()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  
  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.0'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    // maven { url 'https://maven.google.com' }
  }
}


// -- app
android {
  compileSdkVersion 29
  
  defaultConfig {
    applicationId "org.mortalis.wrapkeyboardabc"
    vectorDrawables.useSupportLibrary = true
    
    versionCode 141
    versionName String.format('%d.%d.%d', (int)(versionCode/10/10), (int)(versionCode/10)%10, versionCode%10)
    
    minSdkVersion 16
    targetSdkVersion 29
    
    resValue "string", "app_version", versionName
  }
  
  signingConfigs {
    release {
      storeFile file("release/wrapkeyboard-release-key.jks")
      storePassword "wrapkeyboard"
      keyAlias "wrapkeyboard-alias"
      keyPassword "wrapkeyboard"
    }
  }
  
  buildTypes {
    release {
      minifyEnabled true
      // shrinkResources true
      debuggable false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
  
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs 'src'
      res.srcDirs 'res'
      jniLibs.srcDirs 'libs'
      assets.srcDirs 'assets'
    }

    debug.setRoot('build-types/debug')
    release.setRoot('build-types/release')
  }
  
  lintOptions {
    // checkReleaseBuilds false
    // abortOnError false
    disable "ReferenceType"
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  
  implementation 'com.android.support:appcompat-v7:27.0.2'
  implementation 'com.android.support:support-v4:27.0.2'
  
  implementation 'com.android.support:support-core-utils:27.0.2'
  implementation 'com.android.support:recyclerview-v7:27.0.2'
  implementation 'com.android.support:cardview-v7:27.0.2'
  
  implementation 'com.android.support:design:27.0.2'
  implementation 'com.android.support.constraint:constraint-layout:1.1.0'
  
  implementation 'android.arch.lifecycle:extensions:1.1.0'
  annotationProcessor 'android.arch.lifecycle:compiler:1.1.0'
  
  // implementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
}

task appStart(type: Exec, dependsOn: 'installDebug') {
  commandLine = ["adb", "shell", "monkey", "-p", "${android.defaultConfig.applicationId} 1"]
}

task appStartRelease(type: Exec, dependsOn: 'installRelease') {
  commandLine = ["adb", "shell", "monkey", "-p", "${android.defaultConfig.applicationId} 1"]
}


tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:deprecation'
  // options.compilerArgs << '-Xlint:unchecked'
  // options.deprecation = true
}
